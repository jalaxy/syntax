json        ::= element
value       ::= object | array | string | number | "true" | "false" | "null"
object      ::= '{' ws '}' | '{' members '}'
members     ::= member | member ',' members
member      ::= ws string ws ':' element
array       ::= '[' ws ']' | '[' elements ']'
elements    ::= element | element ',' elements
element     ::= ws value ws
string      ::= '"' character* '"'
characters  ::= "" | character characters
character   ::= [#x0020-#x10FFFF] - '"' - '\' | '\' escape
escape      ::= '"' | '\' | '/' | 'b' | 'f' | 'n' | 'r' | 't' | 'u' hex hex hex hex
hex         ::= digit | [A-Fa-f]
number      ::= integer fraction exponent
integer     ::= digit | onenine digits | '-' digit | '-' onenine digits
digits      ::= digit | digit digits
digit       ::= '0' | onenine
onenine     ::= [1-9]
fraction    ::= "" | '.' digits
exponent    ::= "" | 'E' sign digits | 'e' sign digits
sign        ::= "" | '+' | '-'
ws          ::= [#x20#xA#xD#x9]*