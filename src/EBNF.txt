<程序> ::= <空格>? (<声明> | <函数定义>) (<声明> | <函数定义>)*
<声明> ::= (<变量声明> | <变量定义> | <函数声明>) <空格>?
<变量定义> ::= <类型声明> ';'
<变量声明> ::= 'extern' <空格> <类型声明> ';'
<函数声明> ::= <类型声明> '(' <空格>? (<类型声明> (<空格>? ',' <空格>? <类型声明>)*)? ')' ';'
<函数定义> ::= <类型声明> '(' <空格>? (<类型声明> (<空格>? ',' <空格>? <类型声明>)*)? ')' <空格>? <语句块> <空格>?
<语句块> ::= '{' <空格>? (<声明> | <语句> <空格>?)* '}'
<语句> ::= <if语句> | <while语句> | <return语句> | <表达式> ';'
<if语句> ::= 'if' <空格>? <括号表达式> <空格>? (<语句> | <语句块>)
<while语句> ::= 'while' <空格>? <括号表达式> <空格>? (<语句> | <语句块>)
<return语句> ::= 'return' <空格> <表达式>? ';'
<表达式> ::= <逻辑表达式> ('=' <空格>? <逻辑表达式>)*
<逻辑表达式> ::= <加法表达式> (<RelOp> <空格>? <加法表达式>)*
<RelOp> ::= '<' | '<=' | '>' | '>=' | '==' | '!='
<加法表达式> ::= <乘法表达式> ([+#x2D] <空格>? <乘法表达式>)*
<乘法表达式> ::= <因子> ([*/] <空格>? <因子>)*
<因子> ::=  (<数值> | <括号表达式> | <变量>) <空格>?
<括号表达式> ::= '(' <空格>? <表达式> ')'
<数值> ::= [0-9]+ ('l' | 'u' | 'll' | 'ull')?
<类型声明> ::= <标识符> <空格> <标识符> <空格>?
<变量> ::= <标识符> ('(' <空格>? (<表达式> (',' <空格>? <表达式>)*)? ')')?
<空格> ::= [#x9#xA#xD#x20]+
<关键字> ::= 'if' | 'while' | 'return'
<标识符> ::= [A-Za-z] [0-9A-Za-z_]* - <关键字>
